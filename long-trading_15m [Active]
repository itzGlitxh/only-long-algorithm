// This source code is subject to the terms of the Mozilla Public License 2.0 at https://mozilla.org/MPL/2.0/

//@version=5
var int qty = 3300
strategy(title='LONG-TRADING [15m]', overlay=true, initial_capital=qty, default_qty_type=strategy.cash, default_qty_value=qty, pyramiding=2, commission_type=strategy.commission.cash_per_order, commission_value=0.15)

//---------------------------------------------
// VARIABLES & INITIALIZATION
//---------------------------------------------
tp = input.float(7.5, 'TAKE PROFIT (%)', minval=0, step=0.25)

float step = input.float(0.05, "Radius Step", step=0.001)
float multi = input.float(2.5, "Start Points Distance", step=0.1)

bool trend = na
var float multi1 = 0.0
var float multi2 = 0.0
int n = 3
var float band = 0.0

//---------------------------------------------
// BAND & TREND CALCULATION
//---------------------------------------------
float distance = ta.sma(math.abs(high - low), 100) * multi
float distance1 = ta.sma(math.abs(high - low), 100) * 0.2

if (bar_index == 101)
    trend := true
    band := low * 0.8

if (close < band)
    trend := false

if (close > band)
    trend := true

if (trend[1] == false and ta.change(trend))
    band := low - distance

if (trend[1] == true and ta.change(trend))
    band := high + distance

if (bar_index % n == 0 and trend)
    multi1 := 0.0
    multi2 += step    
    band += distance1 * multi2

if (bar_index % n == 0 and not trend)
    multi1 += step
    multi2 := 0.0   
    band -= distance1 * multi1

Sband = ta.sma(band, n)

color = trend ? #54b6d4 : #cf2b2b

band_upper = ta.sma(band + distance * 0.5, n)
band_lower = ta.sma(band - distance * 0.5, n)
band1 = trend ? band_upper : band_lower

plot(band1, color=bar_index % 2 == 0 ? color.new(chart.fg_color, 50) : na)

plot1 = plot(ta.change(trend) ? na : Sband, style=plot.style_linebr, color=color.gray)
plot2 = plot(ta.sma(hl2, 20), display=display.none)

fill(plot1, plot2, band, ta.sma(hl2, 20), color.new(color, 60), na)

//---------------------------------------------
// STOCHASTIC OSCILLATOR BY COWWATSON
//---------------------------------------------
length = input.int(defval=14, title='Length', group="STOCHASTIC OSCILLATOR")
dLength = input.int(defval=3, title='D Line Length', group="STOCHASTIC OSCILLATOR")
upperTrigger = input.int(defval=80, title='Upper Trigger Line', group="STOCHASTIC OSCILLATOR")
lowerTrigger = input.int(defval=20, title='Lower Trigger Line', group="STOCHASTIC OSCILLATOR")

lowVal = ta.lowest(low, length)
highVal = ta.highest(high, length)
slowK = (close - lowVal) / (highVal - lowVal) * 100
fastD = ta.sma(slowK, dLength)

var float price_adjustment = close / 125

entry_long_mkt_conditions = (
     slowK <= 12.5 - price_adjustment
     and fastD <= 12.5 - price_adjustment
     and (ta.crossunder(slowK, fastD) 
     or ta.crossover(slowK, fastD))
     ) and barstate.isconfirmed

exit_long_mkt_conditions = (
     slowK >= 87.5 + price_adjustment
     and fastD >= 87.5 + price_adjustment
     and (ta.crossunder(slowK, fastD) 
     or ta.crossover(slowK, fastD))
     ) and barstate.isconfirmed

//---------------------------------------------
// CALCULATION OF RISK-ADJUSTED RETURN (Sharpe Ratio)
//---------------------------------------------

annualRiskFreeRate = input.float(0.02, 'Annual Risk-Free Rate', minval=0, step=0.001)
monthlyRiskFreeRate = (annualRiskFreeRate / 12) * 10

lookbackPeriod = 200

logReturns = math.log(close / close[lookbackPeriod])
volatility = ta.stdev(logReturns, lookbackPeriod)
annualizedVolatility = volatility * math.sqrt(10)

profitRate = strategy.netprofit_percent / 100

sharpeRatio = (profitRate - monthlyRiskFreeRate) / annualizedVolatility

var label sharpeLabel = na

if (bar_index % 10 == 0)
    if (na(sharpeLabel))
        sharpeLabel := label.new(x=bar_index, y=low, text="Sharpe Ratio: " + str.tostring(sharpeRatio, "#.##"), style=label.style_label_up, color=color.black, textcolor=color.white, size=size.normal, xloc=xloc.bar_index, yloc=yloc.price)
    else
        label.set_xy(sharpeLabel, bar_index, low)
        label.set_text(sharpeLabel, "Sharpe Ratio: " + str.tostring(sharpeRatio, "#.##"))

//---------------------------------------------
// STRATEGY EXECUTION
//---------------------------------------------
var int qty_entry = 7 * qty / 12
var int qty_reentry = 5 * qty / 12

var float entryPrice = na
var float limitExitPrice = na
var float stopLossPrice = na
var int exitStage = 0
var int reentryStage = 0

var float tp_factor = 1 + tp / 100
var float sl_factor = 1 - tp / 100
var float price_factor = na

var string lastAction = ""

if (entry_long_mkt_conditions[1] and not trend[1] and strategy.opentrades[1] == 0)
    label.new(bar_index[1], close[1] - 0.5, "Upcoming Entry", style=label.style_label_up, color=color.new(color.green, 0), textcolor=color.white)

if entry_long_mkt_conditions and not trend and strategy.opentrades == 0
    entryPrice := (high + low) / 2
    price_factor := (-tp/2 + math.sqrt(math.abs(250 - entryPrice) / tp) + (250 < entryPrice ? -tp/5 : 0)) / 100
    limitExitPrice := math.round(entryPrice * (tp_factor + price_factor), 2)
    stopLossPrice := math.round(entryPrice * (sl_factor - price_factor + (500 < entryPrice ? tp / 250 : -tp / 500)), 2)
    strategy.entry("L1 | " + str.tostring(limitExitPrice) + " | " + str.tostring(stopLossPrice), strategy.long, math.round(qty_entry / entryPrice))
    exitStage := 0
    reentryStage := 0
    lastAction := ""

if close < stopLossPrice
    reentryStage := 1

if reentryStage == 1 and trend and not exit_long_mkt_conditions and strategy.opentrades == 1
    strategy.entry("L2 | " + str.tostring(limitExitPrice), strategy.long, math.round(qty_reentry / entryPrice))
    reentryStage := 0
    lastAction := ""

if exit_long_mkt_conditions and not trend
    exitStage := 1

if exitStage == 1 and not trend and strategy.opentrades > 0
    strategy.exit("CL", limit=limitExitPrice, stop=stopLossPrice/2)
    if lastAction != "exit_created" and close[1] > limitExitPrice * 0.95
        label.new(bar_index[1], close[1] + 0.5, "Limit: " + str.tostring(math.round(close[1]+0.4 + (500 - entryPrice) / 250, 0)), style=label.style_label_down, color=color.new(color.orange, 0), textcolor=color.white)
        lastAction := "exit_created"
else if exitStage == 1 and trend and strategy.opentrades > 0
    strategy.cancel_all()
    exitStage := 2
    if lastAction == "exit_created"
        label.new(bar_index[1], close[1] + 0.5, "Limit Order Cancelled", style=label.style_xcross, color=color.new(color.red, 0), textcolor=color.white)
        lastAction := "exit_cancelled"
else if exitStage == 2 and not trend and strategy.opentrades > 0
    strategy.exit("CL", limit=limitExitPrice, stop=stopLossPrice/2)
    if lastAction != "exit_created" and close[1] > limitExitPrice * 0.95
        label.new(bar_index[1], close[1] + 0.5, "Limit: " + str.tostring(math.round(limitExitPrice+0.4 + (500 - entryPrice) / 250, 0)), style=label.style_label_down, color=color.new(color.orange, 0), textcolor=color.white)
        lastAction := "exit_created"